!
Multichar_Symbols [N] [PN] [PRO] [PSTP] [A] [NUM] [ADV] [V] [VNEG] [CVB] [PTCP] [PART] [INTJ]
	-1SG -1PL -2SG -2PL -3SG -3PL
	.1SG .1PL .2SG .2PL .3SG .3PL .1DU
	-SG -PL
	-NOM -ACC -DAT -GEN -ELA -LOC -INST -PROL -EQT -TERM -DIR -INSO -ABL
	-COLL
	-POSS -EZF -PRED
	-DIM -DIM1 -DIM2 -DIM3  -ATTR -ATTR1 -ATTR2 -ATTR3 -ATTR4
	-VR1 -VR2 -VR3 -VR4 -VR5 -VR6 -VR7 -VR8 -VR9 -AG -CMPR -ATTN -ATTN1 -ATTN2 -ATTN3
	-NR1 -NR2 -NR3 -NR4 -NR5 -NR6 -NR7 -NR8 -NR9 -NR10 -NR11 -NR12 -NR13 -NR14 -NR15 -NR16
	-NDRV1 -NDRV2 -NDRV3
	-ADJR1 -ADJR2 -ADJR3 -ADJR4 ! -ADJR5 -ADJR6 -ADJR7 -ADJR8
	-ADVR
	-INT .AN .INAN .CARD .ORD .MAN .QUAL .SEL -PERS .ABS -DEM .PROX .MIDP .DIST -REFL
	-PASS -RECIP -CAUS1 -CAUS2 -CAUS3 -CAUS4 -CAUS5 -IMP .POL
	-NEG
	-RPST -PRS -PST -FUT
	-HAB .HAB -ITER -ASSUM -CUNC -IMPF -INDR -COND -COND2 -OPT -MAN
	-SUPN -ANT -SIM -CON -LMF -CVB
	=ASS =EMP1 =EMP2 =CLT1 =CLT2
	PNominalStem :Guess ! [NGuess] [AGuess]
	PVerbalStem ! [VGuess]
	@P.POS.pro@
	@D.POS.pro@
LEXICON Root
	Noun;
	Adjective;
	Verb;

LEXICON Noun
	PNominalStem	Tag_N_Guess;

LEXICON Adjective
	PNominalStem	Tag_A_Guess;

LEXICON Verb
	PVerbalStem	Tag_V_Guess;

LEXICON Tag_N_Guess
	[N]%:Guess:0	N_StemBoundary;


LEXICON Tag_A_Guess
	[A]%:Guess:0	A_StemBoundary;

LEXICON Tag_V_Guess
	[V]%:Guess:0	V_StemBoundary;
! Пока только маркируются
LEXICON Tag_ADV
	[ADV]:0		ADV_StemBoundary;

LEXICON ADV_StemBoundary
	-ATTR2:^ҒЫ	Tag_A_ATTR2;
			Enclitic_PART_oq;
			#;
!!! Клитики

LEXICON EncliticBranching
			Enclitic_PART_oq;
			Enclitic_PART_other;
			Enclitic_PART_da;
			#;

! Emphasis
LEXICON Enclitic_PART_oq
	=EMP1:^оқ	Enclitic_PART_da;
	=EMP1:^оқ	#;

LEXICON Enclitic_PART_da
	=CLT1:^-Да	#; ! Д-247 (§163)


LEXICON Enclitic_PART_other
	=CLT2:^-ЛА	#; ! Д-245 (§163)
	=EMP2:^-но	#; ! Усилительная частица, сочетается с глаголами, проверить вариант -ро

! <<< При генерации исключить второй ^оқ после личных аффиксов
LEXICON Endoclitic_PART
	=EMP1:^оқ	V_Slot6;
	=ASS:^ЛА	V_Slot6; ! ЧГ-51 §98
			V_Slot6;
! Междометия
LEXICON Tag_INTJ
	[INTJ]:0	#;
! !!!!!! Существительные :: -NUM > -POSS > -CASE > -PRED
!!! Частеречная помета
! Имя нарицательное
LEXICON Tag_N
	[N]:0	N_StemBoundary;

! Имя собственное
LEXICON Tag_PN
	[PN]:0	N_StemBoundary;

!!! Граница корня/основы
LEXICON N_StemBoundary
	N_Derivation;
	N_Slot1;

! Словообразование
LEXICON N_Derivation
	N_DIM;
	A_Denom_1;
	N_Denom;
	V_Denom;
	ADV_Denom;

!!! Описание «порядков» (Slots)

!!! Порядок 1 : Число
LEXICON N_Slot1
	-SG:0		N_Slot2_SG;
	-PL:^ЛАр	N_Slot2_PL;

!!! Порядок 2 : Притяжательные аффиксы : ЧУ-53-54, Д-50 (§15)
LEXICON N_Slot2_SG
	-POSS.1SG:^Ым		N_Slot3;
	-POSS.2SG:^Ың		N_Slot3; ! <<< Или ^Уң ???
	-POSS.3SG:^ЗУ		N_Slot3;
	-POSS.1PL:^ЫбЫс		N_Slot3;
	-POSS.2PL:^ЛАр^Ың	N_Slot3;
	-POSS.3PL:^ЛАр^ЗЫ	N_Slot3;
				N_Slot3;

LEXICON N_Slot2_PL
	-POSS.1SG:^Ым		N_Slot3;
	-POSS.2SG:^Ың		N_Slot3; ! <<< Или ^Уң ???
	-POSS.3SG:^ЗУ		N_Slot3;
	-POSS.1PL:^ЫбЫс		N_Slot3;
	-POSS.2PL:^Ың		N_Slot3;
	-POSS.3PL:^ЗЫ		N_Slot3;
				N_Slot3;

!!! Порядок 3 : Падежи
LEXICON N_Slot3		! Падеж
	-NOM:0		N_Slot4_Enclitic_Branching;	! также -ACC.INDF и -GEN.INDF : ЧУ-61
	-ACC:^НУ	EncliticBranching;		! ^НЫ ??? -ACC.DEF : ЧУ-61, Д-54 (§19)
	-GEN:^НЫң	A_Denom_2;			! ^НУң ??? -GEN.DEF : ЧУ-61, Д-53 (§18)
	-DAT:^ҒА	EncliticBranching;		! Д-55 (§20)
	-ELA:^НАң	N_Slot4_Enclitic_Branching;	! <^ДАң> также -CMPR : Д-63 (§24)
	-LOC:^ДА	A_Denom_3;			! Д-59 (§22)
	-PROL:^ча	EncliticBranching;		! также -DIR : Д-64 (§25)
	-TERM:^че	EncliticBranching;		! также -CMPR : Д-64 (§26)
	-EQT:^ДЫг	N_Slot4_Enclitic_Branching;	! может рассматриваться как [A] ???
	<"-INST" : ["^" Б (У л) А]>	N_Slot4_Enclitic_Branching; ! Альтернативные формы для -INST (-PROL -COM)
	-INSO:^Ун	EncliticBranching;		! старый -INST : Д-69 (§29), ЧУ-93
	-ABL:^ТЫн	EncliticBranching;		! старый -ABL : Д-69 (§30), ЧУ-93
	-DIR:^ҒАрА	EncliticBranching;		! старый -DIR : Д-69 (§31), ЧУ-93

LEXICON N_Slot4_Enclitic_Branching
			N_Slot4;
			EncliticBranching;

!!! Порядок 4 : Предикативные аффиксы имени : ЧУ-251-253
! >>> Уточнить: -PRED присоединяется к -NOM или основе предыдущего порядка, когда нет -LOC?
LEXICON N_Slot4
	@D.POS.pro@		N_Slot4_affixes;

LEXICON N_Slot4_affixes
	-PRED.1SG:^БЫн	EncliticBranching; ! … ^БЫм : ЧУ-251 … ^қЫм, ^сЫм
	-PRED.2SG:^ЗЫң	EncliticBranching; ! Нет ассимиляции -с + ^ЗЫң >> -сЫң
	<"-PRED" ".3SG": [0 | "^" Д Ы р]>	EncliticBranching; ! Вариант: ^ДЫр < тур : Д-141 (§90)
	-PRED.1PL:^БЫс	EncliticBranching; ! … ^қЫс, ^сЫс
	-PRED.2PL:^ЗААр	EncliticBranching; ! Дополнить описание ассимиляции CVoiceless + ^ЗААр
	-PRED.3PL:^ЛАр	EncliticBranching;

!!! Словообразование

! Уменьшительные аффиксы
LEXICON N_DIM
	-DIM1:^чАҚ		N_DIM_2;
	-DIM2:^АҚ		N_DIM_2;
	-DIM3:^Аш		N_Slot1;

LEXICON N_DIM_2
	-DIM3:^Аш		N_Slot1; ! оолағаш << оол^Ақ^Аш
				N_Slot1;

!! Образование отыменных существительных
LEXICON N_Denom
	-AG:^чУ		N_StemBoundary; ! -AG — деятель : Д-33 §6
	-NDRV1:^ЛЫҚ	N_StemBoundary; ! -NDRV — производное сущ. : Д-32-33 §6
	-NDRV2:^ТАш	N_StemBoundary; ! Д-33 §6
	-NDRV3:^чЫл	N_StemBoundary; ! Д-33 §6

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!! Образование отыменных прилагательных — 1
LEXICON A_Denom_1
	-ATTR1:^ЛЫг	Tag_A_ATTR1; ! Д-73 §33.1 <<< м.б. лучше ^ЛУг ?
	-ATTR2:^ҒЫ	Tag_A_ATTR2; ! Д-73 §33.2

!! Образование отыменных прилагательных — 2 (^ДА^ҒЫ и ^ДЫ^ЙЫ)
!! Прилагательное на ^ДА^ҒЫ может принимать -PRED аффиксы
LEXICON A_Denom_2
	-ATTR2:^ҒЫ	Tag_A_ATTR3;
			N_Slot4;
			#;

LEXICON A_Denom_3
	-ATTR2:^ҒЫ	Tag_A_ATTR4;
			N_Slot4;
			#;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! Образование отыменных глаголов : -VR — verbaliser (Croft)
LEXICON V_Denom
	-VR1:^ЛА	Tag_V;
	-VR2:^А		Tag_V;
	-VR3:^Ар	Tag_V;
	-VR4:^сУн	Tag_V; ! <<< Нет варианта ^зУн ???
	-VR5:^ЗЫрА	Tag_V;
	-VR6:^ЫҚ	Tag_V;
	-VR7:^ҒАр	Tag_V;
	-VR8:^ра	Tag_V; ! <<< Нет варианта ^ре ???

!!! Образование наречий от существительных : древний инструментальный падеж (Д-228 §159)
LEXICON ADV_Denom
	-ADVR:^Ын	Tag_ADV; ! Лучше ^Ун ???


!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!! Прилагательные :: ([-CMRP | -SUP ]) (-NUM > -CASE > -POSS > -PRED)
!!!!!! Д-70 (§32)

LEXICON Tag_A
	[A]:0		A_StemBoundary;

LEXICON A_StemBoundary
			A_Derivation;
			A_Slot1;

! Число прилагательных
LEXICON A_Slot1
	-SG:0		N_Slot3;
	-PL:^ЛАр	N_Slot3;

!!! Прилагательные на ^ЛЫг
LEXICON Tag_A_ATTR1
	[A]:0		A_ATTR1_NUM;

! Число
LEXICON A_ATTR1_NUM
	-SG:0		A_ATTR1_CASE;
	-PL:^ЛАр	A_ATTR1_CASE;

! Падеж
LEXICON A_ATTR1_CASE
	-NOM:0		#;
	-ACC:^НУ	#;
	-GEN:^НЫң	#;
	-DAT:^ҒА	#;
	-ELA:^ДАң	#;
	-LOC:^ДА	#;
	-PROL:^ча	#;
	-TERM:^че	#;
	-EQT:^ДЫг	#;
	<"-INST" : ["^" Б ([Ы | У] л) А]>	#;
	-DIR:^ҒАрА	#;

!!! Прилагательные на ^ҒЫ
LEXICON Tag_A_ATTR2
	[A]:0		A_ATTR2_NUM;

! Число
LEXICON A_ATTR2_NUM
	-SG:0		A_ATTR1_CASE;
	-PL:^ДАр	A_ATTR1_CASE;

! Предикативные аффиксы
LEXICON A_ATTR2_PRED
	-PRED.1SG:^БЫн	EncliticBranching;
	-PRED.2SG:^ЗЫң	EncliticBranching;
	-PRED.3SG:0	EncliticBranching;
	-PRED.1PL:^БЫс	EncliticBranching;
	-PRED.2PL:^ЗААр	EncliticBranching;
	-PRED.3PL:^ЛАр	EncliticBranching;

!!! Притяжательные прилагательные на ^НЫ^йЫ : Д-75 §34
LEXICON Tag_A_ATTR3
	[A]:0		A_GEN_ATTR3_CASE;

! Число выражено в N_Slot1
! Падежи
LEXICON A_GEN_ATTR3_CASE
	-NOM:0		#;
	-ACC:^Н		#;
	-GEN:^НЫң	#;
	-DAT:^н^ҒА	#;
	-LOC:^н^ДА	#;
	-ELA:^НАң	#;

!!! Прилагательные на ^ДА^ҒЫ
LEXICON Tag_A_ATTR4
	[A]:0		A_LOC_ATTR4_NUM;

! Число
LEXICON A_LOC_ATTR4_NUM
	-SG:0		A_ATTR1_CASE; ! A_LOC_ATTR4_CASE;
	-PL:^ДАр	A_ATTR1_CASE; ! A_LOC_ATTR4_CASE;

! Падежи
! LEXICON A_LOC_ATTR4_CASE

!!! Словообразование
LEXICON A_Derivation
	A_Comparatives;
	V_Denom; ! <<< Проверить!
	V_Deadj;

!!! Образование глаголов от прилагательных
LEXICON V_Deadj
	-VR3:^Ар	Tag_V; ! …V^р — Сочетается (только?) с названиями цветов : Д-143 (§93)
	-VR9:^р		Tag_V;

! Степени сравнения
LEXICON A_Comparatives
	<"-CMPR" : ["^" "-" [(а) а р а | а р ы] қ]>	A_Slot1; ! Сравнительная степень -аарақ, -арақ, -арық
	<"-DIM" : ["^" "-" [(а) а р а | а р ы] қ]>	A_Slot1; ! -DIM = -CMPR
	-ATTN1:^УлдУрУм	A_Slot1; ! Слабая степень выраженности
	-ATTN2:^амдық	A_Slot1;
	-ATTN3:^зыбан	A_Slot1;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!! Местоимения

LEXICON Pronoun ! <<< Довести до ума!
	@P.POS.pro@	Tag_PRO;

LEXICON Tag_PRO
	[PRO]:0		Pro_PERS;
	[PRO]:0		Pro_POSS;
	[PRO]:0		Pro_POSS_ABS;
	[PRO]:0		Pro_DEM;
	[PRO]:0		Pro_INT;
	[PRO]:0		Pro_REFL;

! Личные местоимения <<< Довести до ума!
LEXICON Pro_PERS
	-PERS.1SG:0	Pro_PERS_1SG;
	-PERS.2SG:0	Pro_PERS_2SG;
	-PERS.3SG:0	Pro_PERS_3SG;
	-PERS.1PL:пис	N_Slot3;
	<["-PERS" ".2PL"] : [с (и) л е р]>		N_Slot3; ! силер, слер
	<["-PERS" ".3PL"] : [((п) (о | ы) | а) л а р]>	N_Slot3; ! олар, алар, ылар, лар, пылар, плар

LEXICON Pro_PERS_1SG
	-NOM:мен	EncliticBranching; ! #;
	-ACC:мени	#;
	-GEN:меең	#;
	-DAT:ма^ҒА	EncliticBranching; ! #;
	-ELA:менең	#;
	-LOC:мен^ДА	#;
	-INST:меең^БА	#;

LEXICON Pro_PERS_2SG
	-NOM:сен	EncliticBranching; ! #;
	-ACC:сени	#;
	-GEN:сеең	#;
	-DAT:са^ҒА	EncliticBranching; ! #;
	-ELA:сенең	#;
	-LOC:сен^ДА	#;
	-INST:сеең^БА	#;

LEXICON Pro_PERS_3SG
	-NOM:ол		EncliticBranching; ! #;
	-ACC:аны	#;
	-GEN:ааң	#;
	-DAT:а^ҒА	EncliticBranching; ! #;
	-ELA:анаң	#;
	-LOC:ан^ДА	#;
	-INST:ааң^БА	#;
	-EQT:андыг	Enclitic_PART_oq;
! 	-PROL:	#; ! <<< ???
! 	-TERM:	#; ! <<< ???

! Притяжательные местоимения <<< Довести до ума!
LEXICON Pro_POSS
	-POSS.1SG:меең		Pro_POSS_NUM;
	-POSS.2SG:сеең		Pro_POSS_NUM;
	-POSS.3SG:ааң		Pro_POSS_NUM;
	-POSS.1PL:пис^ДЫң	Pro_POSS_NUM;
	-POSS.2PL:силер^ДЫң	Pro_POSS_NUM;
	-POSS.3PL:ылар^ДЫң	Pro_POSS_NUM; ! … пылардың

LEXICON Pro_POSS_NUM
	-SG:0		#;
	-PL:^ЛАр	#;

! Притяжательные местоимения (абсолютные) <<< Довести до ума!
LEXICON Pro_POSS_ABS
	-POSS.ABS.1SG:менЫй^Ы		Pro_POSS_ABS_NUM;
	-POSS.ABS.2SG:сенЫй^Ы		Pro_POSS_ABS_NUM;
	-POSS.ABS.3SG:анЫй^Ы		Pro_POSS_ABS_NUM;
	-POSS.ABS.1PL:пистЫй^Ы		Pro_POSS_ABS_NUM;
	-POSS.ABS.2PL:силердЫй^Ы	Pro_POSS_ABS_NUM;
	-POSS.ABS.3PL:ылардЫй^Ы		Pro_POSS_ABS_NUM; ! … пылардың

LEXICON Pro_POSS_ABS_NUM
	-SG:0		Pro_POSS_ABS_SG_CASE;
	-PL:^ЛАрЫ	N_Slot3;

LEXICON Pro_POSS_ABS_SG_CASE ! <<< см. A_Slot2 ???
	-NOM:0		#;
	-ACC:^Н		#;
	-GEN:^НЫң	#;
	-DAT:^нЫ	#;
	-ELA:^нАң	#;
	-LOC:^ндА	#;

! Указательные местоимения <<< Довести до ума!
LEXICON Pro_DEM
	-DEM.PROX-SG:0		Pro_DEM_PROX_SG;
	-DEM.DIST-SG:0		Pro_PERS_3SG;
	-DEM.PROX-PL:по^нАр	N_Slot3;
	-DEM.DIST-PL:а^нАр	N_Slot3;
	-DEM.MIDP:тиги		N_Slot1; ! <<< -GEN тигидиң ⇒ тигиниң ; -DAT +тигии ; -INST -тигибиле?

LEXICON Pro_DEM_PROX_SG
	-NOM:по		#;
	-ACC:по^нУ	#;
	-GEN:пооң	#;
	-DAT:по^ҒА	EncliticBranching; ! #; ! … поо
	-ELA:мынаң	#;
	-LOC:мында	#;
	-INST:пооң^БА	#;
! 	-TERM:	#; ! <<< ???
! 	-CMPR:	#; ! <<< ???

! Вопросительные местоимения
LEXICON Pro_INT
	-INT.AN:кем		N_Slot3; ! <<< -DAT +кемге -кеме
	-INT.INAN:ноо		N_Slot3; ! <<< -GEN +нооның -ноодың
! 	-INT.:қайы		N_Slot3; ! ‘который, какой?’
	-INT.SEL:қайзы		N_Slot3; ! қайзызы ‘который?’, ‘который из них?’ .SEL (selection)
	-INT.ORD:қанченчи	#; ! ‘который?’ (по счету)
	-INT.CARD:қанче		#; ! ‘сколько?’
	-INT.QUAL:қайдыг	#; ! ‘какой?’ .QUAL (quality)
	-INT.MAN:қайде		#; ! ‘как, каким образом?’ .MAN (manner)

! Лично-возвратные местоимения :: пос~пой
LEXICON Pro_REFL
	-REFL:пос	N_Slot2;
	-REFL:пой	N_Slot2;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!! Числительные

LEXICON Tag_NUM
	[NUM]:0		NUM_Collective;

!!! 60…90
LEXICON Tag_NUM_Tens
	[NUM]:0		NUM_Tens;

LEXICON NUM_Tens
	+он		Tag_NUM;

!!! Образование собирательных числительных
LEXICON NUM_Collective
	-COLL:^АлА	N_Slot2; ! Уточнить последствия: все ли можно образовать от числительных, что можно от существительных?
			N_Slot2;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!!! Послелоги

LEXICON Tag_POST
	[PSTP]:0	#; ! <<< Пока только маркируются. Анализ?
! Частицы
LEXICON Tag_PART
	[PART]:0	#;
!!! Описание «порядков» (Slots)
LEXICON Tag_V
	[V]:0		V_StemBoundary;

! Отрицательный глагол [э + бес] : V_Slot1 и 3—4 не заполняются, V_Slot2 уже заполнен — ???
LEXICON Tag_VNEG
	[VNEG]:0	VNEG_Slot4;

! Граница корня/основы
LEXICON V_StemBoundary
	V_Derivation;
	V_Slot1;
	V_CVB;

! Словообразование
LEXICON V_Derivation
	V_Attenuation;
	N_Deverbal;
	A_Deverbal;

LEXICON V_Attenuation
	-ATTN:^мзЫрА	V_Slot1;

!! Отглагольные существительные
LEXICON N_Deverbal
	-NR1:^Уг	Tag_N;
	-NR2:^Уш	Tag_N;
	-NR3:^ҒУ	Tag_N;
	-NR4:^ҒУш	Tag_N;
	-NR5:^УҚ	Tag_N;
	-NR6:^Ут	Tag_N;
	-NR7:^Ум	Tag_N;
	-NR8:^АҚ	Tag_N;
	-NR9:^л		Tag_N;
	<"-NR10" : ["^" Қ У н]>	Tag_N;
	-NR11:^БАҚ	Tag_N; ! <<< слито из ^пАқ и ^бАқ; образует сущ. типа албақ ‘взятие, взятка’, парбақ ‘езда’
	-NR12:^БА	Tag_N;
	-NR13:^чАҚ	Tag_N;
	-NR14:^чЫҚ	Tag_N;
	-NR15:^ҚАҚ	Tag_N;
	<"-NR16": ["^" [А т ч Ы | А А ч Ы]]>	Tag_N;

!! Отглагольные прилагательные
LEXICON A_Deverbal
	-ADJR1:^чУ	Tag_A;
	-ADJR2:^Ғай	Tag_A;
	-ADJR3:^ҒУр	Tag_A;
	-ADJR4:^Ын	Tag_A;

!!! Порядок 1 : Итератив (1)
LEXICON V_Slot1
	-ITER:^ҒУлА	V_Slot2_1;
			V_Slot2_2;

!!! Залоги (voice): -PASS, -REFL, -RECIP, -CAUS | -PASS и -REFL исключают друг друга ? Возможные сочетания : Д-151 (§99), ЧУ-310-311

!!! Порядок 2 (ITER): -CAUS1 | -CAUS3 | -REFL | -RECIP
LEXICON V_Slot2_1
	-CAUS1:^т	V_Slot4;
	-CAUS3:^Ур	V_Slot4;
	-REFL:^Ун	V_Slot4;
	-RECIP:^Уш	V_Slot4;

!!! Порядок 2 (non-ITER): -CAUS, -PASS, -REFL, -RECIP
LEXICON V_Slot2_2
	-CAUS1:^т	V_Slot3_C2;
	-CAUS2:^ҒУс	V_Slot3_C4;
	-CAUS3:^Ур	V_Slot3_C1;
	-CAUS4:^ДУр	V_Slot3_C1;
	-CAUS4:^ДУр	V_Slot3_ITER;
	-CAUS5:^Ыс	V_Slot4;
	-PASS:^Ул	V_Slot3_C4;
	-PASS:^Ул	V_Slot3_CR; ! <<< Учесть V^л :: См. phonology/rules.xfst > RemExtraV
	-REFL:^Ун	V_Slot3_C4; ! ???
	-REFL:^Ун	V_Slot3_CR; ! <<< Учесть V^н
	-RECIP:^Уш	V_Slot3_C1;
	-RECIP:^Уш	V_Slot3_C4; ! ???
	-RECIP:^Уш	V_Slot3_ITER;
			V_Slot4;

!!! Порядок 3 : -ITER (после -CAUS4 или -RECIP)
LEXICON V_Slot3_ITER
	-ITER:^ҒУлА	V_Slot4; ! <<< м.б. -DIST или -MULT (см. Телякова 2013) ???
			V_Slot4;

!!! Порядок 3 : -CAUS1, -PASS, -REFL (после -CAUS4 или -RECIP)
LEXICON V_Slot3_C1
	-CAUS1:^т	V_Slot4;
	-PASS:^Ул	V_Slot4;
	-REFL:^Ун	V_Slot4;
			V_Slot4;

!!! Порядок 3 : -CAUS2 (после -CAUS1)
LEXICON V_Slot3_C2
	-CAUS2:^ҒУс	V_Slot4;
			V_Slot4;

!!! Порядок 3 : -CAUS3 (после -CAUS2)
LEXICON V_Slot3_C4
	-CAUS4:^ДУр	V_Slot4;
			V_Slot4;

!!! Порядок 3 : -CAUS5, -RECIP (после -PASS или -REFL)
LEXICON V_Slot3_CR
	-CAUS5:^Ыс	V_Slot4;
	-RECIP:^Уш	V_Slot4;
			V_Slot4;

!!! Порядок 4 : Отрицательные формы
LEXICON V_Slot4
	-NEG:^БА	V_Slot5_1;
			V_Slot5_2;
			V_CVB;

!!! Порядок 5 : Время, вид, наклонение… (???)
! При наличии отрицания
LEXICON V_Slot5_1
	-CON:^ҒАн		V_Slot5_3; ! конверб
	-PST:^ҒАн		PCTP_Clitic_Branching; ! -PLP | -PLUP ???
	-RPST:^ДУ		PCTP_Clitic_Branching; ! ЧУ-303, Д-180 (§121), +PERF
	-FUT:^с			PCTP_Clitic_Branching;
	-ASSUM:^ҒА^ДЫг		PCTP_Clitic_Branching; ! -NEG : ^БА^ҒА^ДЫг -> ^БАдЫг
	-HAB:^чАң		PCTP_Clitic_Branching; ! ЧУ-82 или -PST.HAB (Телякова 2012)
	-OPT:^й			PCTP_Clitic_Branching;
	-COND:^ЗА		PCTP_Clitic_Branching; ! ЧУ-311
	-SUPN:^ҒА			#; ! ЧУ-175 <<< EncliticBranching ???
	0:^ҒАн				V_CVB; ! <<< Нужна помета!
					V_IMPER;

! При отсутствии отрицания
LEXICON V_Slot5_2
	-CON:^Ып		V_Slot5_3; ! -CON[CVB]
	-SIM:^А			V_Slot5_5; ! -SIM[CVB]
	-PST:^ҒАн		PCTP_Clitic_Branching; ! -PLP | -PLUP ???
	-RPST:^ДУ		PCTP_Clitic_Branching; ! ЧУ-303, Д-180 (§121), +PERF
	-FUT:^Ар		PCTP_Clitic_Branching; ! Будущее ^Ар -FUT : Д-187 (§124), ЧУ-91
	-ASSUM:^ҒА^ДЫг		PCTP_Clitic_Branching; ! Д-189 (§125) Будущее возможное (ассумптив)
	-HAB:^чАң		PCTP_Clitic_Branching; ! ЧУ-82 или -PST.HAB ? (Телякова 2012)
	-OPT:^ҒАй		PCTP_Clitic_Branching; ! Д-174 (§117) Форма желания (оптатив)
	-COND:^ЗА		PCTP_Clitic_Branching; ! Д-177 (§118), ЧУ-311
	-CUNC:^ҒАлАҚ		PCTP_Clitic_Branching; ! Д-191 (§126) Несовершенное действие — «еще не делал» (кункатив)
	<"-SUPN":["^" А р ("^" Ғ А)]>		#;     ! ЧУ-175 -SUPN : ^Ар | ^Ар^ҒА
						V_IMPER;

! Формы, образуемые от конверба -CON:^Ып либо -NEG-CON:^ҒАн
LEXICON V_Slot5_3
	<"-PRS":["^" ч а (р | т)]>	PCTP_Clitic_Branching; ! ^ча(т) / ^чар : ЧУ-303
	-PRS-INDR:^чат^тУр	PCTP_Clitic_Branching; ! Д-198 §131 < чат(^ып)+тур : ^чат^тыр (->) ^чадыр
	-PST-IMPF:^чат^ҒАн	PCTP_Clitic_Branching; ! ЧУ-304
	-PST-INDR:^ТУр		PCTP_Clitic_Branching; ! Д-200, ЧУ-173 или -INDR ?
	-COND2:^чат^ЗА		PCTP_Clitic_Branching; ! Д-179 (§119) Условная форма сложная
	-MAN:^Ыс		V_Slot5_4; ! Д-216 (§144) Форма решительности действия

! Форма решительности действия
LEXICON V_Slot5_4
	<"-PRS":["^" ч а (р)]>	PCTP_Clitic_Branching;
	-PST:^ҒАн		PCTP_Clitic_Branching;
	-RPST:^ДУ		PCTP_Clitic_Branching;
	-FUT:^Ар		PCTP_Clitic_Branching;
				V_CVB; ! навеяно тарбағлабыза:тарбаг[N]-VR1[V]-CON-MAN-SIM[CVB] ^Ып^Ыс^А

! Формы, образуемые от конверба -SIM:^А
LEXICON V_Slot5_5
	-NPST.HAB:^тУр		PCTP_Clitic_Branching; ! -NEG при помощи чоғул : Д-205 (§134)

!!! Для отрицательного глагола
LEXICON VNEG_Slot4
			Endoclitic_PART;

!! Императивы
LEXICON V_IMPER
	-IMP:0		V_IMP;

LEXICON V_IMP
	<".1SG":["^" [А й (Ы н) | У й У н]]>	#; ! ^УйУн / ^Ай / ^АйЫн : Д-172-173 (§115)
	.2SG:0					#;
	.2SG.POL:^ЗАң				#;
	.3SG:^ЗУн				#; ! ^ЗУн / ^ЗЫн (верх. Мрассу, Кондома : Д-170 (§113))
	.1DU:^Аң				#; ! ‘мы вдвоем’ : Д-173 (§116)
	.1PL:^Аң^ЛАр				#; ! ‘мы многие’ : Д-174
	.2PL:^Лаар				#; ! ^аар после согласных :: Д-169 (§112) + ^ҒЫлА (верх. Мрассу : Д-170)
	.3PL:^ЗУн^ЛАр				#; ! + ^ЗЫн ?

!!!
LEXICON PCTP_Clitic_Branching
			Endoclitic_PART;
			Tag_PTCP;

!!! Порядок 6 : Аффиксы лица и числа
!!! Варианты для различных видовременных форм моделируются в phonology/rules.xfst:VerbPersNum и phonology/rules.xfst:Elision
LEXICON V_Slot6
	-1SG:^БЫм		EncliticBranching;
	-2SG:^ЗЫң		EncliticBranching;
	-3SG:0			EncliticBranching; ! +Вариант: ^ДЫр < тур : Д-141 (§90)
	-1PL:^БЫс		V_Slot6_1PL; ! +Вариант: -FUT:^Ар ^БЫс^ЛАр : ЧУ-93, Д-188 (§124) <<< Глосса ???
	-2PL:^Заар		EncliticBranching;
	-3PL:^ЛАр		EncliticBranching; ! ЧУ-252

LEXICON V_Slot6_1PL
	-PL:^ЛАр		#; ! -1PL-PL ‘мы многие’ : Д-188 (§124)
				EncliticBranching;

!!! Деепричастия / конвербы
LEXICON V_CVB
	-CON:^Уп	Tag_CVB; ! Соединительная либо попутно-предварительная форма : Д-155 (§102)
	-SIM:^А		Tag_CVB; ! Действие, одновременное действию главного глагола : ЧУ-302
	-ANT:^АлА	Tag_CVB; ! Anterior : Действие, после которого немедленно наступает действие главного глагола : ЧУ-302
	-LMF:^ҒАнче	Tag_CVB; ! Деепричастие предела в будущем (ЧУ-303, Оз-239 -LIM.FUT) + ^ҒАнчА (Д-161 §107)
	-CVB:^чат^Ып	Tag_CVB; ! Действие, продолжающееся, пока совершается действие главного глагола : Д-158 (§103)

LEXICON Tag_CVB
	[CVB]:0		EncliticBranching;

!!! Причастия
LEXICON Tag_PTCP
	[PTCP]:0	N_Slot1;


!!! <<<<<<<<<<<<<<<<<<<<<<<<<<
LEXICON Tag_V_Al
	[V]:0		V_AddPlus;

LEXICON V_AddPlus
	+		Verb_MOV;



