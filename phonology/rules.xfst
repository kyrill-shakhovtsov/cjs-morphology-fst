!!! Правила для шорского морфологического анализатора

!!! Морфонемы
define VM [А | У | Ы] ;
define CM [Б | Д | З | Қ | Л | Н | Т] ;

!!! Фонетическая система

! Гласные
define VFront [е | э | и | ӧ | ӱ] ;
define VBack [а | о | у | ы] ;
define VLabialised [о | у | ӧ | ӱ] ;
define VNonLabialised [е | и | а | ы | э] ;
define VWide [а | о | е | э | ӧ] ;
define VNarrow [и | ы | у | ӱ] ;
define V [VFront | VBack | VLabialised | VNonLabialised | VWide | VNarrow] ;

! Согласные
define CVoiceless [к | қ | п | с | т | х | ч | ш] ;
define CVoiced [б | г | ғ | д | ж | з | л | м | н | ң | р | й] ;
define CBack [к | г | ң] ;
define CFront [т | д | ч | с | з | ш | ж | л | н | р] ;
define CMiddle [й] ;
define CLabial [п | б | м | в] ;
define CUvular [қ] ;
define CSpirant [с | ш | з | ғ] ;
define CPlosive [п | б | т | д | к | г] ;
define CAffricate [ч] ;
define CSonorant [й | л | м | н | ң | р] ;
define CNasal [м | н | ң] ;
define CApproximant [л | р | й | г | ғ] ;
define C [CVoiceless | CVoiced | CBack | CFront | CMiddle | CLabial | CUvular | CSpirant | CPlosive | CAffricate | CSonorant | CNasal] ;

!!! Слог
! Типы слогов — за неимением описания шорских взяты хакасские : Б-232
define Syllable C^<3 V+ C^<4 ;

!!! Слово : Д-15 §12
! define Word [Syllable & $[а | ы]] ([Syllable & $[а | ы] & ~$[о | у]]+)
! 	| [Syllable & $[е | э | и]] ([Syllable & $[е | и] & ~$[ӧ | ӱ]]+)
! 	| [Syllable & $[о | у]] ([Syllable & $[а | у] & ~$[о | ы]]+)
! 	| [Syllable & $[ӧ | ӱ]] ([Syllable & $[е | ӱ] & ~$[ӧ | и]]+)
! 	;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! Стяженные формы (выпадение морфемы полностью) <<< что-то можно разобрать на отдельные процессы — элизии и др.
define Contraction л "+" (->) 0 || .#. а _
	.o. Ғ -> 0 || "^" Б А "^" _ А н "^"		! ^БА^ҒАн -> ^БА^Ан : привести к виду А^ҒА -> А^А ???
	.o. "^" Ғ А -> 0 || "^" Б А _ "^" Д Ы г		! ^БА^ҒА^ДЫг -> ^БА^ДЫг
	.o. "^" Ы п -> 0 || V _ "^" т У р
	.o. "^" Ы п (->) 0 || C _ "^" т У р		! Выпадение ^Ып в -PST-ADM после согласного
	.o. "^" Ы п (->) 0 || C _ "^" ч а
	.o. н "^" Ы (->) 0 || "^" Ғ А _ м 		! ^ҒАн^Ым -> ^ҒА^м в -PRT-1SG
	.o. т "^" Ғ А (->) 0 || "^" ч ы _ н		! ^чыт^ҒАн -> ^чын
	.o. а (->) ы || "^" ч _ т "^" Ғ			! <<< перенести !
	.o. Н Ы (->) А || V "^" _ ң			! <<< ???
	.o. ң "^" Ғ (->) "^" й || "^" Н Ы _ Ы		! ^Ың^ҒЫ -> ^НЫ^йЫ
	;

!!! Варианты глагольных аффиксов лица и числа
define VerbPersNum Б -> 0 || "^" А р "^" _ Ы м		! -БЫм -> -Ым
	.o. Б Ы -> 0 || [V | VM] "^" _ м		! -БЫм -> -м
	.o. Б (->) 0 || "^" _ Ы м			! -БЫм -> -Ым
	.o. м (->) н || "^" Б Ы _			! -БЫм -> -БЫн
	.o. Б (->) с || с "^" _ Ы [м | с]		! -БЫм -> -сЫм (ЧУ-251)
	.o. Б (->) Қ || [к | қ] "^" _ Ы [м | с]		! -БЫм -> -ҚЫм (ЧУ-251)
	.o. "^" Ғ А н "^" Б Ы м (->) "^" Ғ А "^" м	! -ҒАн-БЫм -> -ҒАм
	.o. "^" З Ы ң -> "^" ң || [V | VM] _		! -ЗЫң -> -ң
	.o. "^" З А -> 0 || _ "^" З а а р		! -ЗА-Заар -> -Заар
	.o. "^" З а а р (->) "^" З а р || [C | CM] _	! -Заар -> -Зар
	;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! Выпадение гласных на границах морфем
define Elision [V | VM] -> 0 || V "^" _ [л | н]
	.o. А -> 0 || л _ "^" [л А | м д ы к], [V | VM ] "^" _ р ! -FUT по ЧУ-91, в Д-187 сохр. ^Ар после V
	.o. У -> 0, Ы -> 0 || Ы "^" Н _ .#. , [V | VM ] "^" _ ! -POSS [-3SG | -3PL] -ACC : ны -> н : алыптарыны -> алыптарын
	.o. V -> 0 || ~$["^"] V CSonorant _ C "^" А \Қ , ~$["^"] V CSonorant _ "^" Л А ["^" | .#.]! Выпадение последнего гласного основы перед ^А и ^ЛА: ойна < ойун, қурға < қуруг, ӧрле < ӧре
	.o. Ы (->) 0 || "^" м з _ р А			! -мзыра   -> -мзра
	.o. т (->) 0 || "^" ч ы _ "^" Ғ А н		! -чыт-қан -> -чыған
	.o. т (->) 0 || "^" ч а т "^" _ У р		! -чат-тур -> -чадыр
	;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! Выпадение согласных на границах морфем
! Выпадение Ғ
define GDrop [г | ғ] -> 0 || V _ "^" Ғ А
	.o. Ғ -> 0 || [г | ғ] "^" _ А , А "^" _ А [C | .#.]
	.o. Ғ (->) 0 || ғ [а | е | А] "^" _ А .#.
	.o. Ғ (->) 0 || Syllable+ [CNasal | а | е | ғ] "^" _ А ! факультативно: аға -> аа в -DAT, -PRT
	.o. Ғ (->) 0 || $[Syllable] [CNasal | ӱ] "^" _ А .#. ! Выпадение Ғ в -POSS [-1SG | -2SG | -2PL ] -DAT <<< …позымға ???
	;

! Выпадение Л
define LDrop Л -> 0 || C "^" _ а а р ;

! Выпадение р
define RDrop р (->) 0 || "^" А _ "^" Ғ А ;

! Выпадение З
define ZDrop З -> 0 || C "^" _ [Ы | У] [.#. | "^"] ;

!!! Ғ -> й между гласными
define GJConv г -> й, ғ -> й || V _ "^" Ы .#. ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! Аугментация (наращение начальных/конечных фонем)
define Augmentation [..] -> "^" н || "^" [З У | Л А р "^" Ы]  _ "^" [Ғ | Д] А ["^" | .#.] ! -POSS{-3SG|-3PL}н{-DAT|-LOC}
	.o. [..] (->) "^" Ы || CPlosive _ "^" ч А Қ	! Напр.: қап + ^чақ -> қабычақ : ЕФ-р.3
	;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! Ассимиляция Б, Ғ, Д, З, Қ, Л, Н, Т
define BA Б -> б || [[CVoiced - CNasal] | V] "^" _
	.o. Б -> м || CNasal "^" _
	.o. Б -> п || [ CVoiceless | Қ ] "^" _ ;

define GA Ғ -> ғ || [CVoiced | VBack] "^" _ VBack
	.o. Ғ -> г || [CVoiced | VFront] "^" _ VFront
	.o. Ғ -> қ || CVoiceless "^" _ VBack
	.o. Ғ -> к || CVoiceless "^" _ VFront ;

define DA Д -> д || [CVoiced | V] "^" ("-") _
	.o. Д -> н || CNasal "^" _ ! [а | е] ң
	.o. Д -> т || CVoiceless "^" ("-") _
	.o. Д -> Н || "^" [З | Л А р ] Ы "^" _ А ң ! -POSS [-3SG | -3PL] -ELA
	;

define ZA З -> з || [CVoiced | V] "^" _
	.o. З -> с || CVoiceless "^" _ ;

define KA Қ -> қ || VBack _ , _ VBack
	.o. Қ -> к || VFront _ , _ VFront ;

define LA Л -> л || [[CVoiced - CNasal] | V] "^" _
	.o. Л -> н || CNasal "^" _
	.o. Л -> т || CVoiceless "^" _ ;

define NA Н -> н || [V | CNasal] "^" _
	.o. Н -> т || CVoiceless "^" _
	.o. Н -> д || CApproximant "^" _ ;

define TA Т -> т || [р | CVoiceless] "^" _
	.o. Т -> д || [CVoiced - р] "^" _ ;

define LpA л -> п || а _ "+" п
	.o. л -> к || а _ "+" к ;

define Assimilation BA .o. DA .o. TA .o. KA .o. GA .o. LA .o. NA .o. ZA .o. LpA	;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! См. lexicon/numeral.lexc  ([20…50] 60…90) и morphotactics/nominal.lexc:NUM_Tens
!!! См. lexicon/verb.lexc:Verb_Al
define DropPlus (V) "+" -> "^" || C _ о н ["^" | .#.]	! Числительные [20…50] 60…90
	.o. "+" -> 0 || .#. а C _ !			! ал + глагол движения
	;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! Гармония гласных

define VowelHarmony А -> а, Ы -> ы || VBack ~$[V] _
	.o. А -> е, Ы -> и || VFront ~$[V] _
	.o. У (->) и || VFront ~$[V] _
	.o. У (->) ы || VBack ~$[V] _
	.o. У -> ы || [VBack & VNonLabialised] ~$[V] _
	.o. У -> и || [VFront & VNonLabialised] ~$[V] _
	.o. У -> у || [VBack & VLabialised] ~$[V] _
	.o. У -> ӱ || [VFront & VLabialised] ~$[V] _ ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! Озвончение интервокальных глухих на границах морфем
define Voicing к -> г, қ -> ғ, с -> з, п -> б, т -> д, ш -> ж || V (й) ("^") _ "^" V ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! Удаление символа границы морфем "^", окончательная подчистка

define CleanUp V -> 0 || _ "^" о қ ["^" | .#.]
	.o. г -> ғ || VBack _ "^" ! г -> ғ на границе морфем — перенести ?
	.o. "^" -> 0
	.o. е -> ӱ || ӱ _ .#. ! <<< ad-hoc для слов типа пӧрӱге ~ пӧрӱӱ
	.o. е -> и || и _ ң .#. ! <<< ad-hoc для слов типа кижиниң ~ кижииң
	;
