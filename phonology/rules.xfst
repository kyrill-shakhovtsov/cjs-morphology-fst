!!! Правила для шорского морфологического анализатора

!!! Фонетическая система

! Гласные
define VFront [е | э | и | ӧ | ӱ] ;
define VBack [а | о | у | ы] ;
define VLabialised [о | у | ӧ | ӱ] ;
define VNonLabialised [е | и | а | ы | э] ;
define VWide [а | о | е | э | ӧ] ;
define VNarrow [и | ы | у | ӱ] ;
define V [VFront | VBack | VLabialised | VNonLabialised | VWide | VNarrow] ;

! Согласные
define CVoiceless [к | қ | п | с | т | х | ч | ш] ;
define CVoiced [б | г | ғ | д | ж | з | л | м | н | ң | р | й] ;
define CBack [к | г | ң] ;
define CFront [т | д | ч | с | з | ш | ж | л | н | р] ;
define CMiddle [й] ;
define CLabial [п | б | м | в] ;
define CUvular [қ] ;
define CSpirant [с | ш | з | ғ] ;
define CPlosive [п | б | т | д | к | г] ;
define CAffricate [ч] ;
define CSonorant [й | л | м | н | ң | р] ;
define CNasal [м | н | ң] ;
define CApproximant [л | р | й | г | ғ] ;
define C [CVoiceless | CVoiced | CBack | CFront | CMiddle | CLabial | CUvular | CSpirant | CPlosive | CAffricate | CSonorant | CNasal] ;

!!! Слог
! Типы слогов — за неимением описания шорских взяты хакасские : Б-232
define Syllable C^<3 V+ C^<4 ;

!!! Слово : Д-15 §12
! define Word [Syllable & $[а | ы]] ([Syllable & $[а | ы] & ~$[о | у]]+)
! 	| [Syllable & $[е | э | и]] ([Syllable & $[е | и] & ~$[ӧ | ӱ]]+)
! 	| [Syllable & $[о | у]] ([Syllable & $[а | у] & ~$[о | ы]]+)
! 	| [Syllable & $[ӧ | ӱ]] ([Syllable & $[е | ӱ] & ~$[ӧ | и]]+)
! 	;

!!! Морфонемы
define VM [А | У | Ы] ;
define CM [Б | Д | З | Қ | Л | Н | П | Т] ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! Стяженные формы (предварительный вариант)
define Contraction Ғ -> 0 || "^" Б А "^" _ А н "^"	! "^" Б А "^" Ғ А "^" Д Ы г -> "^" Б А "^" Д Ы г
	.o. "^" Ғ А -> 0 || "^" Б А _ "^" Д Ы г
	.o. "^" Ы п -> 0 || V _ "^" Т У р
	.o. "^" Ы п (->) 0 || C _ "^" Т У р		! Выпадение -ып в -PST-ADM после согласного
	.o. "^" Ы п (->) 0 || C _ "^" ч а
	.o. н "^" Ы (->) 0 || "^" Ғ А _ м 		! ^ҒАн^Ым -> ^ҒА^м в -PRT-1SG
	.o. т "^" Ғ А (->) 0 || "^" ч ы _ н		! ^чыт^ҒАн -> ^чын
	.o. Ы (->) 0 || "^" м з _ р А
	.o. а (->) ы || "^" ч _ т "^" Ғ
	.o. т (->) 0 || "^" ч ы _ "^" Ғ А н, "^" ч а _ "^" Т У р ! ^чат^қан -> ^чыған, ^чат^тыр -> ^ча^дыр
	.o. Н Ы (->) А || V "^" _ ң			! <<< ???
	.o. ң "^" Ғ (->) "^" й || "^" Н Ы _ Ы		! ^Ың^ҒЫ -> ^НЫ^йЫ
	;

!!! Выпадение гласных на границах морфем
define RemExtraV [V | VM] -> 0 || V "^" _ [л | н]
	.o. А -> 0 || л _ "^" [л А | м д ы к], V "^" _ р
	.o. У -> 0, Ы -> 0 || [V | VM ] "^" _ ;

!!! Выпадение последнего гласного основы перед ^А и ^ЛА: ойна < ойун, қурға < қуруг, ӧрле < ӧре
define DropStemV V -> 0 || ~$["^"] V CSonorant _ C "^" А , ~$["^"] V CSonorant _ "^" Л А; ! ~$["^"] V CSonorant _ ("^") C "^" А

!!! Протетические звуки
define Prostesis [..] -> н || "^" [З Ы | Л А р "^" Ы]  _ "^" [Ғ | Д] А ["^" | .#.] ! -POSS [-3SG | -3PL] н [-DAT | -LOC]
	.o. [..] -> У || [C ("^") [C | CM]] "^" _ п "^" ч а
	.o. Д -> Н || "^" [З | Л А р ] Ы "^" _ А ң ! -POSS [-3SG | -3PL] -ELA (Найти более логичное место?)
	;

!!! Выпадение звуков на границах морфем
! Выпадение Ғ
define GDrop [г | ғ] -> 0 || V _ "^" Ғ А
	.o. Ғ -> 0 || А "^" _ А [C | .#.]
	.o. Ғ (->) 0 || ғ [а | е | А] "^" _ А .#.
	.o. Ғ (->) 0 || Syllable^>1 [CNasal | а | е | ғ] "^" _ А ! факультативно: аға -> аа в -DAT, -PRT
	.o. Ғ (->) 0 || $[Syllable] [CNasal | ӱ] "^" _ А .#. ; ! Выпадение Ғ в -POSS [-1SG | -2SG | -2PL ] -DAT <<< …позымға ???

! Выпадение Л
define LDrop Л -> 0 || C "^" _ а а р ;

! Выпадение р
define RDrop р (->) 0 || "^" А _ "^" Ғ А ;

! Выпадение У и Ы : -POSS [-3SG | -3PL] -ACC : ны -> н : алыптарыны -> алыптарын
define UYDrop [Ы | У] -> 0 || Ы "^" Н _ .#. , V "^" _ ;

! Выпадение З
define ZDrop З -> 0 || C "^" _ Ы [.#. | "^"] ;

! Ғ -> й
define GJConv г -> й, ғ -> й || V _ "^" Ы .#. ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! Ассимиляция согласных б, ғ, д, з, л, н
define BA Б -> б || [[CVoiced - CNasal] | V] "^" _
	.o. Б -> м || CNasal "^" _
! 	.o. Б (->) Ғ || қ "^" _ [ы | и] [м | с] ! ^БЫм (->) ^қЫм, ^сЫм : ЧУ-251
	.o. Б -> п || CVoiceless "^" _ ;

define GA Ғ -> ғ || [CVoiced | VBack] "^" _ VBack
	.o. Ғ -> г || [CVoiced | VFront] "^" _ VFront
	.o. Ғ -> қ || CVoiceless "^" _ VBack
	.o. Ғ -> к || CVoiceless "^" _ VFront ;

define DA Д -> д || [CVoiced | V] "^" ("-") _
	.o. Д -> н || CNasal "^" _ ! [а | е] ң
	.o. Д -> т || CVoiceless "^" ("-") _ ;

define ZA З -> з || [CVoiced | V] "^" _
	.o. З -> с || CVoiceless "^" _ ;

define KA Қ -> қ || VBack _
	.o. Қ -> к || VFront _ ;

define LA Л -> л || [[CVoiced - CNasal] | V] "^" _
	.o. Л -> н || CNasal "^" _
	.o. Л -> т || CVoiceless "^" _ ;

define NA Н -> н || [V | CNasal] "^" _
	.o. Н -> т || CVoiceless "^" _
	.o. Н -> д || CApproximant "^" _ ;

define TA Т -> т || [CSonorant | CVoiceless] "^" _
	.o. Т -> д || [CVoiced - CSonorant] "^" _ ;

define Assimilation BA .o. DA .o. TA .o. GA .o. KA .o. LA .o. NA .o. ZA ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! Гармония гласных

define VowelHarmony А -> а, Ы -> ы || VBack ~$[V] _
	.o. А -> е, Ы -> и || VFront ~$[V] _
	.o. У (->) и || VFront ~$[V] _
	.o. У (->) ы || VBack ~$[V] _
	.o. У -> ы || [VBack & VNonLabialised] ~$[V] _
	.o. У -> и || [VFront & VNonLabialised] ~$[V] _
	.o. У -> у || [VBack & VLabialised] ~$[V] _
	.o. У -> ӱ || [VFront & VLabialised] ~$[V] _ ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! Озвончение интервокальных глухих на границах морфем
define Voicing к -> г, қ -> ғ, с -> з, п -> б, т -> д, ш -> ж || [V | VM] (й) ("^") _ "^" V ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!! Удаление символа границы морфем "^", окончательная подчистка

define CleanUp V -> 0 || _ "^" о қ ["^" | .#.]
	.o. "^" -> 0
	.o. е -> ӱ || ӱ _ .#. ; ! <<< ad-hoc для слов типа пӧрӱге ~ пӧрӱӱ
