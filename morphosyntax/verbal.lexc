!!! Описание «порядков» (Slots)
LEXICON Tag_V
	[V]:0		V_StemBoundary;

! Отрицательный глагол [э + бес] : V_Slot1 и 3—4 не заполняются, V_Slot2 уже заполнен — ???
LEXICON Tag_VNEG
	[VNEG]:0	VNEG_Slot4;

! Граница корня/основы
LEXICON V_StemBoundary
	V_Derivation;
	V_Slot1;
	V_CVB;

! Словообразование
LEXICON V_Derivation
	V_Attenuation;
	N_Deverbal;
	A_Deverbal;

LEXICON V_Attenuation
	-ATTN:^мзЫрА	V_Slot1;

!! Отглагольные существительные
LEXICON N_Deverbal
	-NR1:^Уг	Tag_N; ! ғ ???
	-NR2:^Уш	Tag_N;
	-NR3:^ҒУ	Tag_N;
	-NR4:^ҒУш	Tag_N;
	-NR5:^Уқ	Tag_N;
	-NR6:^Ут	Tag_N;
	-NR7:^Ум	Tag_N;
	-NR8:^Ақ	Tag_N;
	-NR9:^л		Tag_N;
	-NR10:^қУн	Tag_N;
	-NR11:^БАқ	Tag_N; ! <<< слито из ^пАқ + ^бАқ
	-NR12:^БА	Tag_N;
	-NR13:^чАқ	Tag_N;
	-NR14:^чЫқ	Tag_N;

!! Отглагольные прилагательные
LEXICON A_Deverbal
	-ADJR1:^қАқ	Tag_A;
	-ADJR2:^чУ	Tag_A;
	-ADJR3:^Ғай	Tag_A;
	-ADJR4:^Ғур	Tag_A;
	-ADJR5:^Ын	Tag_A;
! 	-ADJR6:^бАқ	Tag_A; ! <<< образует сущ. типа албақ ‘взятие, взятка’, парбақ ‘езда’
! 	-ADJR7:^ғы	Tag_A; ! <<< образует сущ. типа суғалғы < суг алғы ‘место, где берут воду’, ташалғы ‘место, где берут камень’
	<"-ADJR8": ["^" А т ч Ы | А А ч ы]>	Tag_A;

! Порядок 1 : Итератив 1
LEXICON V_Slot1
	-ITER:^ҒУлА	V_Slot2_2;
			V_Slot2_1;

!!! Залоги (voice): -PASS, -REFL, -RECIP, -CAUS | -PASS и -REFL исключают друг друга ?
!! Возможные сочетания см.: Д-151 (§99), ЧУ-310-311
! Порядок 2 (non-ITER): -CAUS | -PASS | -REFL | -RECIP
LEXICON V_Slot2_1	! -CAUS | -PASS | -REFL | -RECIP
	-CAUS1:^т	V_Slot3_C2;
	-CAUS2:^ҒУс	V_Slot3_C4;
	-CAUS3:^Ур	V_Slot3_C1;
	-CAUS4:^ДУр	V_Slot3_C1;
	-CAUS4:^ДУр	V_Slot3_ITER;
	-CAUS5:^Ыс	V_Slot4;
	-PASS:^Ул	V_Slot3_CR; ! <<< Учесть V^л :: См. phonology/rules.xfst > RemExtraV
	-REFL:^Ун	V_Slot3_CR; ! <<< Учесть V^н
	-RECIP:^Уш	V_Slot3_C1;
	-RECIP:^Уш	V_Slot3_C4;
	-RECIP:^Уш	V_Slot3_ITER;

LEXICON V_Slot2_2
	-CAUS1:^т	V_Slot4;
	-CAUS3:^Ур	V_Slot4;
	-REFL:^Ун	V_Slot4;
	-RECIP:^Уш	V_Slot4;

! -ITER может быть как до, так и после одиночного -CAUS, может ли быть -ITER-CAUS-CAUS, -CAUS-ITER-CAUS или -CAUS-CAUS-ITER ???
! Какой -CAUS может предшествовать -ITER ???
LEXICON V_Slot3_ITER
	-ITER:^ҒУлА	V_Slot4; ! <<< м.б. -DIST или -MULT (см. Телякова:2013) ???
			V_Slot4;

! Порядок 2 : -CAUS | -PASS | -REFL | -RECIP
LEXICON V_Slot3_C1
	-CAUS1:^т	V_Slot4;
	-PASS:^Ул	V_Slot4;
	-REFL:^Ун	V_Slot4;
			V_Slot4;

LEXICON V_Slot3_C2
	-CAUS2:^ҒУс	V_Slot4;
			V_Slot4;

LEXICON V_Slot3_C4
	-CAUS4:^ДУр	V_Slot4;
			V_Slot4;

LEXICON V_Slot3_CR
	-CAUS5:^Ыс	V_Slot4;
	-RECIP:^Уш	V_Slot4;
			V_Slot4;

! Порядок 3 : Отрицательные формы
LEXICON V_Slot4
	-NEG:^БА	V_Slot5_2NEG;
			V_Slot5_2POS;
			V_CVB;

LEXICON V_Slot5_2POS
	-CON:^Ып		V_Slot5_3; ! -CON[CVB]
	-SIM:^А			V_Slot5_4; ! -SIM[CVB]
	-PST:^ҒАн		PCTP_Clitic_Branching; ! -PLP | -PLUP ???
	-RPST:^ДУ		PCTP_Clitic_Branching; ! ЧУ-303, Д-180 (§121), +PERF
	-FUT:^Ар		PCTP_Clitic_Branching; ! Будущее ^Ар -FUT : Д-187 (§124), ЧУ-91
	-ASSUM:^ҒА^ДЫг		PCTP_Clitic_Branching; ! Д-189 (§125) Будущее возможное (ассумптив)
	-HAB:^чАң		PCTP_Clitic_Branching; ! ЧУ-82 или -PST.HAB ? (Телякова 2012)
	-OPT:^ҒАй		PCTP_Clitic_Branching; ! Д-174 (§117) Форма желания (оптатив)
	-COND:^ЗА		PCTP_Clitic_Branching; ! Д-177 (§118), ЧУ-311
	-CUNC:^ҒАлАҚ		PCTP_Clitic_Branching; ! Д-191 (§126) Несовершенное действие — «еще не делал» (кункатив)
	<"-SUPN":["^" А р ("^" Ғ А)]>	    #; ! -SUPN : ^Ар | ^Ар^ҒА
				V_IMPER;

LEXICON V_Slot5_2NEG
	-CON:^ҒАн		V_Slot5_3; ! конверб
	-PST:^ҒАн		PCTP_Clitic_Branching; ! -PLP | -PLUP ???
	-RPST:^ДУ		PCTP_Clitic_Branching; ! ЧУ-303, Д-180 (§121), +PERF
	-FUT:^с			PCTP_Clitic_Branching;
	-ASSUM:^ҒА^ДЫг		PCTP_Clitic_Branching; ! -NEG : ^БА^ҒА^ДЫг -> ^БАдЫг
	-HAB:^чАң		PCTP_Clitic_Branching; ! ЧУ-82 или -PST.HAB (Телякова 2012)
	-OPT:^й			PCTP_Clitic_Branching;
	-COND:^ЗА		PCTP_Clitic_Branching; ! ЧУ-311
	-SUPN:^ҒА			#; ! ЧУ-175 <<< EncliticBranching ???
	0:^ҒАн				V_CVB; ! <<< Нужна помета!
					V_IMPER;

LEXICON V_Slot5_3
	<"-PRS":["^" ч а (р)]>	PCTP_Clitic_Branching; ! ^ча / ^чар : ЧУ-303
	-PRS-EVID:^чат^ТУр	PCTP_Clitic_Branching; ! Д-198 §131 < чат(^ып)+тур : ^чат^тыр (->) ^чадыр
	-PST-IMPF:^чат^ҒАн	PCTP_Clitic_Branching; ! ЧУ-304
	-PST-ADM:^ТУр		PCTP_Clitic_Branching; ! Д-200, ЧУ-173 или -EVID ?

LEXICON V_Slot5_4
	-FUT.ITER:^ТУр		PCTP_Clitic_Branching; ! -NEG при помощи чоғул : Д-205 (§134)

! Для отрицательного глагола
LEXICON VNEG_Slot4
			Endoclitic_PART;

!! Императивы
LEXICON V_IMPER
	-IMP:0		V_IMP;

LEXICON V_IMP
	<".1SG":["^" [А й (Ы н) | У й У н]]>	#; ! ^УйУн / ^Ай / ^АйЫн : Д-172-173 (§115)
	.2SG:0					#;
	.2SG.POL:^ЗАң				#;
	.3SG:^ЗУн				#; ! ^ЗУн / ^ЗЫн (верх. Мрассу, Кондома : Д-170 (§113))
	.1DU:^Аң				#; ! ‘мы вдвоем’ : Д-173 (§116)
	.1PL:^Аң^ЛАр				#; ! ‘мы многие’ : Д-174
	.2PL:^Лаар				#; ! ^аар после согласных :: Д-169 (§112) + ^ГЫлА (верх. Мрассу : Д-170)
	.3PL:^ЗУн^ЛАр				#; ! + ^ЗЫн ?

!!!
LEXICON PCTP_Clitic_Branching
			Endoclitic_PART;
			Tag_PTCP;

!!! Порядок 6 : Аффиксы лица и числа
!!! Варианты для различных видовременных форм моделируются в phonology/rules.xfst:VerbPersNum
LEXICON V_Slot6
	-1SG:^БЫм		EncliticBranching; ! … ^БЫм : ЧУ-251 … ^қЫм, ^сЫм
	-2SG:^ЗЫң		EncliticBranching;
	-3SG:0			EncliticBranching; ! Вариант: ^ДЫр < тур : Д-141 (§90)
	-1PL:^БЫс		EncliticBranching; ! … ^қЫс, ^сЫс
	-2PL:^Заар		EncliticBranching; ! Дополнить описание ассимиляции CVoiceless + ^Заар
	-3PL:^ЛАр		EncliticBranching; ! ЧУ-252

!!! Деепричастия / конвербы
LEXICON V_CVB
	-CON:^Уп	Tag_CVB; ! Соединительная либо попутно-предварительная форма : Д-155 (§102)
	-SIM:^А		Tag_CVB; ! Действие, одновременное действию главного глагола : ЧУ-302
	-ANT:^АлА	Tag_CVB; ! Anterior : Действие, после которого немедленно наступает действие главного глагола : ЧУ-302
	-LMF:^ҒАнче	Tag_CVB; ! Деепричастие предела в будущем (ЧУ-303, Оз-239 -LIM.FUT) + ^ҒАнчА (Д-161 §107) ? ^ҒАн^чА
	-CVB:^чат^Ып	Tag_CVB; ! Действие, продолжающееся, пока совершается действие главного глагола : Д-158 (§103)

LEXICON Tag_CVB
	[CVB]:0		EncliticBranching;

!!! Причастия
LEXICON Tag_PTCP
	[PTCP]:0	N_Slot1;
